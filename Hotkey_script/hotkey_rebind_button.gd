class_name Hotkey_Rebind_Button
extends Control

@onready var label = $HBoxContainer/Label as Label
@onready var button = $HBoxContainer/Button as Button

@export var action_name : String = "ui_left"

func _ready():
	set_process_unhandled_key_input(false)
	set_action_name()
	set_text_for_key()

func set_action_name() -> void:
	label.text = "unassigned"
	
	match action_name:
		"ui_left":
			label.text = "Move Left"
		"ui_right":
			label.text = "Move Right"
		"ui_up":
			label.text = "Jump"

func set_text_for_key() -> void:
	var action_events = InputMap.action_get_events(action_name)
	if action_events.size() > 0:
		# Assuming we only want the first event for now
		for event in action_events:
			if event is InputEventKey:  # Ensure the event is a key event
				var action_keycode = OS.get_keycode_string(event.keycode)
				button.text = action_keycode  # Set button text to the key representation
				return  # Exit after setting the first available key
	else:
		button.text = "Not Assigned"  # If no events assigned
	
func _on_button_toggled(button_pressed):
	if button_pressed:
		button.text = "Press any key..."
		set_process_unhandled_key_input(button_pressed)
		
		for i in get_tree().get_nodes_in_group("hotkey_button"):
			if i.action_name != self.action_name:
				i.button.toggle_mode = false
				i.set_process_unhandled_key_input(false)
	else:
		for i in get_tree().get_nodes_in_group("hotkey_button"):
			if i.action_name != self.action_name:
				i.button.toggle_mode = true
				i.set_process_unhandled_key_input(false)
				
				
		set_text_for_key()
		

func _unhandled_key_input(event):
	rebind_action_key(event)
	button.button_pressed = false
	

func rebind_action_key(event) -> void:
	InputMap.action_erase_events(action_name)
	InputMap.action_add_event(action_name, event)
	
	
	set_process_unhandled_key_input(false)
	set_text_for_key()
	set_action_name()
